####### Terraform #######

Generamos claves ssh

ssh-keygen -t rsa -b 4096 -f ~/.ssh/azure_vm_key

Creamos un directorio para el terraform

mkdir Terraform_Azure
cd Terraform_Azure

Creamos el main.tf y agregamos el codigo de infraestructura:
nano main.tf

main.tf

terraform {
  required_providers {
    azurerm = {
      source  = "hashicorp/azurerm"
      version = "~> 3.110.0"   # última estable
    }
  }

  required_version = ">= 1.1.0"
}

provider "azurerm" {
  features {
    resource_group {
      prevent_deletion_if_contains_resources = false
    }
  }
}

# Grupo de recursos
resource "azurerm_resource_group" "rg" {
  name     = "TerraFormSOIIIRG"
  location = "canadacentral"
}

# Red virtual
resource "azurerm_virtual_network" "vnet" {
  name                = "TFSOIIIVnet"
  address_space       = ["10.0.0.0/16"]
  location            = azurerm_resource_group.rg.location
  resource_group_name = azurerm_resource_group.rg.name
}

# Subnet (con dependencia explícita de la VNet)
resource "azurerm_subnet" "subnet" {
  name                 = "TFSOIIISubnet"
  resource_group_name  = azurerm_resource_group.rg.name
  virtual_network_name = azurerm_virtual_network.vnet.name
  address_prefixes     = ["10.0.1.0/24"]

  depends_on = [azurerm_virtual_network.vnet]
}
# Network Security Group
resource "azurerm_network_security_group" "nsg" {
  name                = "TFSOIIINSG"
  location            = azurerm_resource_group.rg.location
  resource_group_name = azurerm_resource_group.rg.name

  security_rule {
    name                       = "SSH"
    priority                   = 1001
    direction                  = "Inbound"
    access                     = "Allow"
    protocol                   = "Tcp"
    source_port_range          = "*"
    destination_port_range     = "22"
    source_address_prefix      = "*"
    destination_address_prefix = "*"
  }
}

# Asociar NSG a la Subnet
resource "azurerm_subnet_network_security_group_association" "subnet_assoc" {
  subnet_id                 = azurerm_subnet.subnet.id
  network_security_group_id = azurerm_network_security_group.nsg.id
}

# IP pública (Standard + Static)
resource "azurerm_public_ip" "pip" {
  name                = "TFSOIIIPublicIP"
  location            = azurerm_resource_group.rg.location
  resource_group_name = azurerm_resource_group.rg.name
  allocation_method   = "Static"
  sku                 = "Standard"
}

# NIC con IP pública
resource "azurerm_network_interface" "nic" {
  name                = "TFSOIIINIC"
  location            = azurerm_resource_group.rg.location
  resource_group_name = azurerm_resource_group.rg.name

  ip_configuration {
    name                          = "internal"
    subnet_id                     = azurerm_subnet.subnet.id
    private_ip_address_allocation = "Dynamic"
    public_ip_address_id          = azurerm_public_ip.pip.id
  }
}

# Máquina virtual Linux
resource "azurerm_linux_virtual_machine" "vm" {
  name                = "OS3VM"
  resource_group_name = azurerm_resource_group.rg.name
  location            = azurerm_resource_group.rg.location
  size                = "Standard_B2as_v2"
  admin_username      = "alangarcia"

  network_interface_ids = [
    azurerm_network_interface.nic.id,
  ]

  os_disk {
    caching              = "ReadWrite"
    storage_account_type = "Standard_LRS"
  }

  source_image_reference {
    publisher = "Canonical"
    offer     = "0001-com-ubuntu-server-jammy"
    sku       = "22_04-lts-gen2"
    version   = "latest"
  }

  admin_ssh_key {
    username   = "alangarcia"
    public_key = file("~/.ssh/azure_vm_key.pub")
  }
}

# Output para mostrar la IP pública
output "vm_public_ip" {
  description = "Dirección IP pública de la máquina virtual"
  value       = azurerm_public_ip.pip.ip_address
}

Ejecutamos los comandos de terraform para iniciar el despliegue

terraform init

Luego ejecutamos

terraform plan

Y luego al confirmar todo

Terraform apply (seteamos yes)


Si da error por la VNet, ejecutar este comando

terraform import azurerm_subnet.subnet "/subscriptions/9145c68b-76f8-4b62-9061-174a25eea61f/resourceGroups/TFSOIII/providers/Microsoft.Network/virtualNetworks/TFSOIIIVnet/subnets/TFSOIIIVnet"

#########################
